plugins {
    id 'java'
    id "io.freefair.lombok" version "6.5.1"
    id 'maven-publish'
    id 'signing'
}

group 'io.github.singlerr'
archivesBaseName = 'largess-strings'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.0"
}

java{
    withSourcesJar()
    withJavadocJar()
}


publishing{
    repositories {
        maven {
            name = "sonatype"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials{
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
    publications{

        mavenJava(MavenPublication){
            groupId = project.group
            artifactId = rootProject.name
            version = project.version

            from components.java

            pom{
                name = rootProject.name
                description = "A simple java string library"
                url = "https://github.com/Singlerr/largess-strings"

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'singlerr'
                        name = 'Ryu Seowoong'
                        email = 'singlerr@naver.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Singlerr/largess-strings.git'
                    developerConnection = 'scm:git:ssh://github.com/Singlerr/largess-strings.git'
                    url = "https://github.com/Singlerr/largess-strings"
                }
            }

        }
    }

}


signing {
    sign publishing.publications.mavenJava
}

